module QSymbolics

using ..QuantumSavory: AbstractRepresentation, AbstractUse

using Symbolics
import Symbolics: simplify
using SymbolicUtils
import SymbolicUtils: Symbolic, _isone, flatten_term, isnotflat, Chain, Fixpoint
using TermInterface
import TermInterface: istree, exprhead, operation, arguments, similarterm, metadata

using LinearAlgebra
import LinearAlgebra: eigvecs

import QuantumOpticsBase
import QuantumOpticsBase: tensor, ‚äó, basis, Ket, Bra, Operator, Basis, SpinBasis, projector # TODO make QuantumInterface
import QuantumOptics
import QuantumClifford
import QuantumClifford: MixedDestabilizer, Stabilizer, @S_str

export ‚äó,
       X,Y,Z,œÉÀ£,œÉ ∏,œÉ·∂ª,
       H,CNOT,CPHASE,
       X1,X2,Y1,Y2,Z1,Z2,X‚ÇÅ,X‚ÇÇ,Y‚ÇÅ,Y‚ÇÇ,Z‚ÇÅ,Z‚ÇÇ,
       SProjector,MixedState,IdentityOp,StabilizerState,@S_str

function countmap(samples) # A simpler version of StatsBase.countmap, because StatsBase is slow to import
    counts = Dict{Any,Any}()
    for s in samples
        counts[s] = get(counts, s, 0)+1
    end
    counts
end

function countmap_flatten(samples, flattenhead)
    counts = Dict{Any,Any}()
    for s in samples
        if istree(s) && s isa flattenhead # TODO Could you use the TermInterface `operation` here instead of `flattenhead`?
            coef, term = arguments(s)
            counts[term] = get(counts, term, 0)+coef
        else
            counts[s] = get(counts, s, 0)+1
        end
    end
    counts
end

function num_to_sub(n::Int)
    str = string(n)
    replace(str,
        "1"=>"‚ÇÅ",
        "2"=>"‚ÇÇ",
        "3"=>"‚ÇÉ",
        "4"=>"‚ÇÑ",
        "5"=>"‚ÇÖ",
        "6"=>"‚ÇÜ",
        "7"=>"‚Çá",
        "8"=>"‚Çà",
        "9"=>"‚Çâ",
        "0"=>"‚ÇÄ",
    )
end

##

const CacheType = Dict{Tuple{<:AbstractRepresentation,<:AbstractUse},Any}
mutable struct Metadata
    express_cache::CacheType # TODO use more efficient mapping
end
Metadata() = Metadata(CacheType())
macro withmetadata(strct)
    withmetadata(strct)
end
function withmetadata(strct)
    @assert strct.head == :struct
    struct_name = strct.args[2]
    if struct_name isa Expr
        struct_name = struct_name.args[1]
    end
    struct_args = strct.args[end].args
    if all(x->x isa Symbol || x isa LineNumberNode || x.head==:(::), struct_args)
        # add constructor
        constructor = :($struct_name() = new())
        args = [x for x in struct_args if x isa Symbol || x isa Expr]
        append!(constructor.args[1].args, args)
        append!(constructor.args[end].args[end].args, args)
        push!(constructor.args[end].args[end].args, :(Metadata()))
        push!(struct_args, constructor)
    else
        # modify constructor
        newwithmetadata.(struct_args)
    end
    # add metadata slot
    push!(struct_args, :(metadata::Metadata))
    esc(quote
        Base.@__doc__ $strct
        metadata(x::$struct_name)=x.metadata
    end)
end
function newwithmetadata(expr::Expr)
    if expr.head==:call && expr.args[1]==:new
        push!(expr.args, :(Metadata()))
    else
        newwithmetadata.(expr.args)
    end
end
newwithmetadata(x) = x

# TODO use Latexify for these
Base.show(io::IO, ::MIME"text/latex", x::Symbolic{Ket}) = print(io, x)
Base.show(io::IO, ::MIME"text/latex",  x::Symbolic{Operator}) = print(io, x)

##

const SymbolicKetOrOperator = Symbolic{<:Union{Ket,Operator}}
Base.:(-)(x::SymbolicKetOrOperator) = (-1)*x
Base.:(-)(x::SymbolicKetOrOperator,y::SymbolicKetOrOperator) = x + (-y)

function Base.isequal(x::X,y::Y) where {X<:SymbolicKetOrOperator, Y<:SymbolicKetOrOperator}
    if X==Y
        if istree(x)
            if operation(x)==operation(y)
                ax,ay = arguments(x),arguments(y)
                (length(ax) == length(ay)) && all(zip(ax,ay)) do xy isequal(xy...) end
            else
                false
            end
        else
            propsequal(x,y)
        end
    else
        false
    end
end

# TODO check that this does not cause incredibly bad runtime performance
# use a macro to provide specializations if that is indeed the case
propsequal(x,y) = all(n->getproperty(x,n)==getproperty(y,n), propertynames(x))

struct SKet <: Symbolic{Ket}
    name::Symbol
    basis::Basis # From QuantumOpticsBase # TODO make QuantumInterface
end
istree(::SKet) = false
metadata(::SKet) = nothing
Base.print(io::IO, x::SKet) = print(io, "|$(x.name)‚ü©")
basis(x::SKet) = x.basis

struct SOperator <: Symbolic{Operator}
    name::Symbol
    basis::Basis # From QuantumOpticsBase # TODO make QuantumInterface
end
istree(::SOperator) = false
metadata(::SOperator) = nothing
Base.print(io::IO, x::SOperator) = print(io, "$(x.name)")
basis(x::SOperator) = x.basis

@withmetadata struct SScaledKet <: Symbolic{Ket}
    coeff
    ket
    SScaledKet(c,k) = _isone(c) ? k : new(c,k)
end
istree(::SScaledKet) = true
arguments(x::SScaledKet) = [x.coeff, x.ket]
operation(x::SScaledKet) = *
Base.:(*)(c, x::Symbolic{Ket}) = SScaledKet(c,x)
Base.:(*)(x::Symbolic{Ket}, c) = SScaledKet(c,x)
Base.:(/)(x::Symbolic{Ket}, c) = SScaledKet(1/c,x)
function Base.print(io::IO, x::SScaledKet)
    if x.coeff isa Number
        print(io, "$(x.coeff)$(x.ket)")
    else
        print(io, "($(x.coeff))$(x.ket)")
    end
end
basis(x::SScaledKet) = basis(x.ket)

@withmetadata struct SScaledOperator <: Symbolic{Operator}
    coeff
    operator
    SScaledOperator(c,k) = _isone(c) ? k : new(c,k)
end
istree(::SScaledOperator) = true
arguments(x::SScaledOperator) = [x.coeff, x.operator]
operation(x::SScaledOperator) = *
Base.:(*)(c, x::Symbolic{Operator}) = SScaledOperator(c,x)
Base.:(*)(x::Symbolic{Operator},c) = SScaledOperator(c,x)
Base.:(/)(x::Symbolic{Operator}, c) = SScaledOperator(1/c,x)
function Base.print(io::IO, x::SScaledOperator)
    if x.coeff isa Number
        print(io, "$(x.coeff)$(x.operator)")
    else
        print(io, "($(x.coeff))$(x.operator)")
    end
end
basis(x::SScaledOperator) = basis(x.operator)

@withmetadata struct SAddKet <: Symbolic{Ket}
    dict
    SAddKet(d) = length(d)==1 ? SScaledKet(reverse(first(d))...) : new(d)
end
istree(::SAddKet) = true
arguments(x::SAddKet) = [SScaledKet(v,k) for (k,v) in pairs(x.dict)]
operation(x::SAddKet) = +
Base.:(+)(xs::Symbolic{Ket}...) = SAddKet(countmap_flatten(xs, SScaledKet))
Base.print(io::IO, x::SAddKet) = print(io, "("*join(map(string, arguments(x)),"+")*")")
basis(x::SAddKet) = basis(first(x.dict).first)

@withmetadata struct SAddOperator <: Symbolic{Operator}
    dict
    SAddOperator(d) = length(d)==1 ? SScaledOperator(reverse(first(d))...) : new(d)
end
istree(::SAddOperator) = true
arguments(x::SAddOperator) = [SScaledOperator(v,k) for (k,v) in pairs(x.dict)]
operation(x::SAddOperator) = +
Base.:(+)(xs::Symbolic{Operator}...) = SAddOperator(countmap_flatten(xs, SScaledOperator))
Base.print(io::IO, x::SAddOperator) = print(io, "("*join(map(string, arguments(x)),"+")*")")
basis(x::SAddOperator) = basis(first(x.dict).first)

@withmetadata struct STensorKet <: Symbolic{Ket}
    terms
    function STensorKet(terms)
        coeff, cleanterms = prefactorscalings(terms)
        coeff * new(cleanterms)
    end
end
istree(::STensorKet) = true
arguments(x::STensorKet) = x.terms
operation(x::STensorKet) = ‚äó
‚äó(xs::Symbolic{Ket}...) = STensorKet(collect(xs))
Base.print(io::IO, x::STensorKet) = print(io, join(map(string, arguments(x)),""))
basis(x::STensorKet) = tensor(basis.(x.terms)...)

@withmetadata struct STensorOperator <: Symbolic{Operator}
    terms
    function STensorOperator(terms)
        coeff, cleanterms = prefactorscalings(terms)
        coeff * new(cleanterms)
    end
end
istree(::STensorOperator) = true
arguments(x::STensorOperator) = x.terms
operation(x::STensorOperator) = ‚äó
‚äó(xs::Symbolic{Operator}...) = STensorOperator(collect(xs))
Base.print(io::IO, x::STensorOperator) = print(io, join(map(string, arguments(x)),"‚äó"))
basis(x::STensorOperator) = tensor(basis.(x.terms)...)

@withmetadata struct SApplyKet <: Symbolic{Ket}
    op
    ket
end
istree(::SApplyKet) = true
arguments(x::SApplyKet) = [x.op,x.ket]
operation(x::SApplyKet) = *
Base.:(*)(op::Symbolic{Operator}, k::Symbolic{Ket}) = SApplyKet(op,k)
Base.print(io::IO, x::SApplyKet) = begin print(io, x.op); print(io, x.ket) end
basis(x::SApplyKet) = basis(x.ket)

@withmetadata struct SBraKet <: Symbolic{Complex}
    bra
    op
    ket
end
istree(::SBraKet) = true
arguments(x::SBraKet) = [x.bra,x.op,x.ket]
operation(x::SBraKet) = *
#Base.:(*)(b::Symbolic{Bra}, op::Symbolic{Operator}, k::Symbolic{Ket}) = SBraKet(b,op,k)
function Base.print(io::IO, x::SBraKet)
    if isnothing(x.op)
        print(io,string(x.bra)[1:end-1])
        print(io,x.ket)
    else
        print(io.x.bra)
        print(io.x.op)
        print(io.x.ket)
    end
end

Base.show(io::IO, x::Symbolic{Ket}) = print(io,x)
Base.show(io::IO, x::Symbolic{Operator}) = print(io,x)

function hasscalings(xs)
    any(xs) do x
        operation(x) == *
    end
end

""" Used to perform (a*|k‚ü©) ‚äó (b*|l‚ü©) ‚Üí (a*b) * (|k‚ü©‚äó|l‚ü©) """
function prefactorscalings(xs)
    terms = []
    coeff = 1::Any
    for x in xs
        if istree(x) && operation(x) == *
            c,t = arguments(x)
            coeff *= c
            push!(terms,t)
        else
            push!(terms,x)
        end
    end
    coeff, terms
end

function prefactorscalings_rule(xs)
    coeff, terms = prefactorscalings(xs)
    coeff * ‚äó(terms...)
end

function isnotflat_precheck(*)
    function (x)
        operation(x) === (*) || return false
        args = arguments(x)
        for t in args
            if istree(t) && operation(t) === (*)
                return true
            end
        end
        return false
    end
end

FLATTEN_RULES = [
    @rule(~x::isnotflat_precheck(‚äó) => flatten_term(‚äó, ~x)),
    @rule ‚äó(~~xs::hasscalings) => prefactorscalings_rule(xs)
]

tensor_simplify = Fixpoint(Chain(FLATTEN_RULES))

##

abstract type SpecialKet <: Symbolic{Ket} end
istree(::SpecialKet) = false
basis(x::SpecialKet) = x.basis

@withmetadata struct XBasisState <: SpecialKet
    idx::Int
    basis::Basis
end
Base.print(io::IO, x::XBasisState) = print(io, "|X$(num_to_sub(x.idx))‚ü©")

@withmetadata struct YBasisState <: SpecialKet
    idx::Int
    basis::Basis
end
Base.print(io::IO, x::YBasisState) = print(io, "|Y$(num_to_sub(x.idx))‚ü©")

@withmetadata struct ZBasisState <: SpecialKet
    idx::Int
    basis::Basis
end
Base.print(io::IO, x::ZBasisState) = print(io, "|Z$(num_to_sub(x.idx))‚ü©")

@withmetadata struct FockBasisState <: SpecialKet
    idx::Int
    basis::Basis
end
Base.print(io::IO, x::FockBasisState) = print(io, "|$(num_to_sub(x.idx))‚ü©")

@withmetadata struct DiscreteCoherentState <: SpecialKet
    alpha::Number # TODO parameterize
    basis::Basis
end
Base.print(io::IO, x::DiscreteCoherentState) = print(io, "|$(x.alpha)‚ü©")

@withmetadata struct ContinuousCoherentState <: SpecialKet
    alpha::Number # TODO parameterize
    basis::Basis
end
Base.print(io::IO, x::ContinuousCoherentState) = print(io, "|$(x.alpha)‚ü©")

@withmetadata struct MomentumEigenState <: SpecialKet
    p::Number # TODO parameterize
    basis::Basis
end
Base.print(io::IO, x::MomentumEigenState) = print(io, "|Œ¥‚Çö($(x.p))‚ü©")

@withmetadata struct PositionEigenState <: SpecialKet
    x::Float64 # TODO parameterize
    basis::Basis
end
Base.print(io::IO, x::PositionEigenState) = print(io, "|Œ¥‚Çì($(x.x))‚ü©")

const qubit_basis = SpinBasis(1//2)
"""Basis state of œÉÀ£"""
const X1 = const X‚ÇÅ = XBasisState(1, qubit_basis)
"""Basis state of œÉÀ£"""
const X2 = const X‚ÇÇ = XBasisState(2, qubit_basis)
"""Basis state of œÉ ∏"""
const Y1 = const Y‚ÇÅ = YBasisState(1, qubit_basis)
"""Basis state of œÉ ∏"""
const Y2 = const Y‚ÇÇ = YBasisState(2, qubit_basis)
"""Basis state of œÉ·∂ª"""
const Z1 = const Z‚ÇÅ = ZBasisState(1, qubit_basis)
"""Basis state of œÉ·∂ª"""
const Z2 = const Z‚ÇÇ = ZBasisState(2, qubit_basis)

##

abstract type AbstractSingleQubitGate <: Symbolic{Operator} end
abstract type AbstractTwoQubitGate <: Symbolic{Operator} end
istree(::AbstractSingleQubitGate) = false
istree(::AbstractTwoQubitGate) = false
basis(::AbstractSingleQubitGate) = SpinBasis(1//2)
basis(::AbstractTwoQubitGate) = SpinBasis(1//2)‚äóSpinBasis(1//2)

@withmetadata struct OperatorEmbedding <: Symbolic{Operator}
    gate::Symbolic{Operator} # TODO parameterize
    indices::Vector{Int}
    basis::Basis
end
istree(::OperatorEmbedding) = true

@withmetadata struct XGate <: AbstractSingleQubitGate end
eigvecs(g::XGate) = [X1,X2]
Base.print(io::IO, ::XGate) = print(io, "XÃÇ")
@withmetadata struct YGate <: AbstractSingleQubitGate end
eigvecs(g::YGate) = [Y1,Y2]
Base.print(io::IO, ::YGate) = print(io, "YÃÇ")
@withmetadata struct ZGate <: AbstractSingleQubitGate end
eigvecs(g::ZGate) = [Z1,Z2]
Base.print(io::IO, ::ZGate) = print(io, "ZÃÇ")
@withmetadata struct HGate <: AbstractSingleQubitGate end
Base.print(io::IO, ::HGate) = print(io, "HÃÇ")
@withmetadata struct CNOTGate <: AbstractTwoQubitGate end
Base.print(io::IO, ::CNOTGate) = print(io, "CÃÇNOT")
@withmetadata struct CPHASEGate <: AbstractTwoQubitGate end
Base.print(io::IO, ::CPHASEGate) = print(io, "CÃÇPHASE")

"""Pauli X operator, also available as the constant `œÉÀ£`"""
const X = const œÉÀ£ = XGate()
"""Pauli Y operator, also available as the constant `œÉ ∏`"""
const Y = const œÉ ∏ = YGate()
"""Pauli Z operator, also available as the constant `œÉ·∂ª`"""
const Z = const œÉ·∂ª = ZGate()
"""Hadamard gate"""
const H = HGate()
"""CNOT gate"""
const CNOT = CNOTGate()
"""CPHASE gate"""
const CPHASE = CPHASEGate()

##

"""Projector for a given ket

```jldoctest
julia> SProjector(X1‚äóX2)
ùêè[|X‚ÇÅ‚ü©|X‚ÇÇ‚ü©]

julia> express(SProjector(X2))
Operator(dim=2x2)
  basis: Spin(1/2)
  0.5+0.0im  -0.5-0.0im
 -0.5+0.0im   0.5+0.0im
```"""
@withmetadata struct SProjector <: Symbolic{Operator}
    ket::Symbolic{Ket} # TODO parameterize
end
istree(::SProjector) = true
arguments(x::SProjector) = [x.ket]
operation(x::SProjector) = projector
QuantumOptics.projector(x::Symbolic{Ket}) = SProjector(x)
basis(x::SProjector) = basis(x.ket)
function Base.print(io::IO, x::SProjector)
    print(io,"ùêè[")
    print(io,x.ket)
    print(io,"]")
end

"""Completely depolarized state

```jldoctest
julia> MixedState(X1‚äóX2)
ùïÑ

julia> express(MixedState(X1‚äóX2))
Operator(dim=4x4)
  basis: [Spin(1/2) ‚äó Spin(1/2)]sparse([1, 2, 3, 4], [1, 2, 3, 4], ComplexF64[0.25 + 0.0im, 0.25 + 0.0im, 0.25 + 0.0im, 0.25 + 0.0im], 4, 4)

  express(MixedState(X1‚äóX2), CliffordRepr())
  Rank 0 stabilizer

  ‚îÅ‚îÅ‚îÅ‚îÅ
  + X_
  + _X
  ‚îÅ‚îÅ‚îÅ‚îÅ

  ‚îÅ‚îÅ‚îÅ‚îÅ
  + Z_
  + _Z
```"""
@withmetadata struct MixedState <: Symbolic{Operator}
    basis::Basis # From QuantumOpticsBase # TODO make QuantumInterface
end
MixedState(x::Symbolic{Ket}) = MixedState(basis(x))
MixedState(x::Symbolic{Operator}) = MixedState(basis(x))
istree(::MixedState) = false
basis(x::MixedState) = x.basis
Base.print(io::IO, x::MixedState) = print(io, "ùïÑ")

"""The identity operator for a given basis

```judoctest
julia> IdentityOp(X1‚äóX2)
ùïÄ

julia> express(IdentityOp(Z2))
Operator(dim=2x2)
  basis: Spin(1/2)sparse([1, 2], [1, 2], ComplexF64[1.0 + 0.0im, 1.0 + 0.0im], 2, 2)
```"""
@withmetadata struct IdentityOp <: Symbolic{Operator}
    basis::Basis # From QuantumOpticsBase # TODO make QuantumInterface
end
IdentityOp(x::Symbolic{Ket}) = IdentityOp(basis(x))
IdentityOp(x::Symbolic{Operator}) = IdentityOp(basis(x))
istree(::IdentityOp) = false
basis(x::IdentityOp) = x.basis
Base.print(io::IO, x::IdentityOp) = print(io, "ùïÄ")

"""State defined by a stabilizer tableau

```jldoctest
julia> StabilizerState(S"XX ZZ")
ùíÆ‚ÇÇ

julia> express(StabilizerState(S"-X"))
Ket(dim=2)
  basis: Spin(1/2)
  0.7071067811865475 + 0.0im
 -0.7071067811865475 + 0.0im
```"""
@withmetadata struct StabilizerState <: Symbolic{Ket}
    stabilizer::MixedDestabilizer
end
function StabilizerState(x::Stabilizer)
    r,c = size(x)
    @assert r==c
    StabilizerState(MixedDestabilizer(x))
end
istree(::StabilizerState) = false
basis(x::StabilizerState) = SpinBasis(1//2)^QuantumClifford.nqubits(x.stabilizer)
Base.print(io::IO, x::StabilizerState) = print(io, "ùíÆ$(num_to_sub(QuantumClifford.nqubits(x.stabilizer)))")

end
